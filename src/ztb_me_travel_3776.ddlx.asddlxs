@Metadata.layer: #CORE
@Search.searchable: true
@UI: {
    headerInfo: {
        typeName: 'Travel',
        typeNamePlural: 'Travels',
        title: {
            type: #STANDARD,
            value: 'Travel_ID'
        }}
}
annotate view ZTB_C_TRAVEL_3776 with
{
  @UI.facet: [ { id:              'Travel',
                 purpose:         #STANDARD,
                 type:            #IDENTIFICATION_REFERENCE,
                 label:           'Travel',
                 position:        10 },
               { id:              'Booking',
                 purpose:         #STANDARD,
                 type:            #LINEITEM_REFERENCE,
                 label:           'Booking',
                 position:        20,
                 targetElement:   '_Booking'}]


  @UI: {
      lineItem:       [ { position: 10, importance: #HIGH } ,
                      { type: #FOR_ACTION, dataAction: 'copyTravel', label: 'Copy Travel' } ],
      identification: [ { position: 10, label: 'Travel ID' } ] }
  @Search.defaultSearchElement: true
  travel_id;

  @UI: {
    lineItem: [{ position: 20, importance: #HIGH }],
    identification: [{ position: 20}],
    selectionField: [{ position: 20 }]}
  @Consumption.valueHelpDefinition: [{ entity: {name: '/DMO/I_Agency', element: 'AgencyID' } }]
  agency_id;

  @UI: {
    lineItem: [{ position: 30, importance: #HIGH }],
    identification: [{ position: 30}],
    selectionField: [{ position: 30 }]}
  @Consumption.valueHelpDefinition: [{ entity: {name: '/DMO/I_Customer', element: 'CustomerID' } }]
  @Search.defaultSearchElement: true
  customer_id;

  @UI: {
    lineItem: [ { position: 40, importance: #MEDIUM } ],
    identification: [ { position: 40 } ] }
  begin_date;

  @UI: {
    lineItem: [{ position: 41, importance: #MEDIUM } ],
    identification: [ { position: 41 } ] }
  end_date;

  @UI: {
    identification: [ { position: 42 } ] }
  booking_fee;

  @UI: {
    lineItem: [{ position: 43, importance: #MEDIUM }],
    identification: [{ position: 43, label: 'Total Price'}]}
  total_price;

  @Consumption.valueHelpDefinition: [{ entity: {name: 'I_Currency', element: 'Currency' } }]
  currency_code;

  @UI: {
  lineItem: [ { position: 50, importance: #HIGH },
  { type: #FOR_ACTION, dataAction: 'createTravelByTemplate', label: 'Create Travel by Template' } ],
  identification: [ { position: 45, label: 'Status [O(Open)|A(Accepted)|X(Canceled)]' } ] }
  @Consumption.valueHelpDefinition: [{ entity: { name: '/DMO/I_Overall_Status_VH', element: 'OverallStatus' }}]
  travel_status;

  //@UI.hidden: true
  //overallstatustext;

  @UI: {
      identification:[ { position: 46 } ]  }
  description;

  @UI.hidden: true
  last_changed_at;
  @UI: {
  lineItem: [ { position: 60, importance: #HIGH } ] }
  @EndUserText.label: 'Discount 10%'
  discountprice;

    
}